// SPDX-License-Identifier: GPL-3.0-or-later
/// @notice Safe ETH and ERC20 transfer library that gracefully handles missing return values.
/// @author Solady (https://github.com/vectorized/solady/blob/main/src/utils/SafeTransferLib.sol)
pragma solidity 0.8.30;

library TransferHelper {
    /// @dev The ETH transfer has failed.
    error ETHTransferFailed();

    /// @dev The ERC20 `transfer` has failed.
    error TransferFailed();

    /// @dev The ERC20 `transferFrom` has failed.
    error TransferFromFailed();

    /// @dev Sends `amount` of ERC20 `token` from the current contract to `to`. Reverts upon failure.
    /// IMPORTANT: if amount == 0, bypass the transfer.
    function safeTransfer(address token, address to, uint256 amount) internal {
        if (amount != 0) {
            if (token != address(0)) {
                // ERC20 transfer
                /// @solidity memory-safe-assembly
                assembly {
                    mstore(0x14, to) // Store the `to` argument.
                    mstore(0x34, amount) // Store the `amount` argument.
                    mstore(0x00, 0xa9059cbb000000000000000000000000) // `transfer(address,uint256)`.
                    // Perform the transfer, reverting upon failure.
                    let success := call(gas(), token, 0, 0x10, 0x44, 0x00, 0x20)
                    if iszero(and(eq(mload(0x00), 1), success)) {
                        if iszero(lt(or(iszero(extcodesize(token)), returndatasize()), success)) {
                            mstore(0x00, 0x90b8ec18) // `TransferFailed()`.
                            revert(0x1c, 0x04)
                        }
                    }
                    mstore(0x34, 0) // Restore the part of the free memory pointer that was overwritten.
                }
            } else {
                // ETH transfer
                /// @solidity memory-safe-assembly
                assembly {
                    if iszero(call(gas(), to, amount, codesize(), 0x00, codesize(), 0x00)) {
                        mstore(0x00, 0xb12d13eb) // `ETHTransferFailed()`.
                        revert(0x1c, 0x04)
                    }
                }
            }
        }
    }

    /// @dev Sends `amount` of ERC20 `token` from `from` to `to`.
    /// Reverts upon failure.
    ///
    /// The `from` account must have at least `amount` approved for
    /// the current contract to manage.
    function safeTransferFrom(address token, address from, address to, uint256 amount) internal {
        if (amount != 0) {
            /// @solidity memory-safe-assembly
            assembly {
                let m := mload(0x40) // Cache the free memory pointer.
                mstore(0x60, amount) // Store the `amount` argument.
                mstore(0x40, to) // Store the `to` argument.
                mstore(0x2c, shl(96, from)) // Store the `from` argument.
                mstore(0x0c, 0x23b872dd000000000000000000000000) // `transferFrom(address,address,uint256)`.
                let success := call(gas(), token, 0, 0x1c, 0x64, 0x00, 0x20)
                if iszero(and(eq(mload(0x00), 1), success)) {
                    if iszero(lt(or(iszero(extcodesize(token)), returndatasize()), success)) {
                        mstore(0x00, 0x7939f424) // `TransferFromFailed()`.
                        revert(0x1c, 0x04)
                    }
                }
                mstore(0x60, 0) // Restore the zero slot to zero.
                mstore(0x40, m) // Restore the free memory pointer.
            }
        }
    }
}
